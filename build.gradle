//Easy semantic version based off describe + tags
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--dirty', '--long'
        standardOutput = stdout
    }
    def tagVer = stdout.toString().trim()
    def semVer = (tagVer =~ /v([\d\.]+)/)[0][1]

    if (tagVer.contains('dev')) {
        semVer += "-SNAPSHOT"
    }
    return semVer
}

description = "Kryptnostic libraries for fully homomorphic encryption."
group = "com.kryptnostic"
version = getVersionName()

allprojects { 
    apply plugin: "maven-publish"
    apply plugin: "java"
    apply plugin: "maven"
    apply plugin: "idea"

    task wrapper(type: Wrapper) {
        gradleVersion = '1.10'
    }

    task sourceJar (type : Jar) {
      classifier = 'sources'
      from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn:javadoc) {
      classifier = 'javadoc'
      from javadoc.destinationDir
    }

    ext.isReleaseVersion = false

    ext.sourceCompatibility=JavaVersion.VERSION_1_6
    ext.targetCompatibility=JavaVersion.VERSION_1_6

    ext.jackson_version='2.3.0'
}



