def stdout = new ByteArrayOutputStream()
exec {
    commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
    standardOutput = stdout
}
def branch=stdout.toString().trim();

if ( (branch.equals("develop") || branch.equals("master") ) && ( System.getenv("bamboo_JAR_SIGNING_KEYRING") != null) && ( System.getenv("bamboo_JAR_SIGNING_PASSWORD")!=null ) && ( System.getenv("bamboo_SONATYPE_USERNAME")!=null ) && ( System.getenv("bamboo_SONATYPE_PASSWORD")!=null ) ) {
    
    apply plugin: "maven-publish"
    apply plugin: "signing"

    ext {
        snapshotUrl = "https://oss.sonatype.org/content/repositories/snapshots"
        releaseUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
        localUrl = "file:/${project.projectDir}/artifacts"
    }

    ext."signing.keyId" = 'FDEB5A45'
    ext."signing.secretKeyRingFile" = "$System.env.bamboo_JAR_SIGNING_KEYRING" 
    ext."signing.password" = "$System.env.bamboo_JAR_SIGNING_PASSWORD"

    configurations {
        pom
    }

    signing {
        sign configurations.archives
    }

    signing {
        sign configurations.pom
    }

    publishing {
        repositories {
            maven { 
                url snapshotUrl
                credentials {
                    username = "$System.env.bamboo_SONATYPE_USERNAME"
                    password = "$System.env.bamboo_SONATYPE_PASSWORD"
                }
            }
        }

        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact ('README.md') {
                    classifier = 'README'
                    extension  = 'md'
                }

                artifact javadocJar
                artifact sourcesJar

                pom.withXml {
                    asNode().children().last() + {
                        resolveStrategy = Closure.DELEGATE_FIRST
                        name 'fhe-core'
                        description 'Kryptnostic test libraries'
                        url 'https://www.github.com/kryptnostic/fhe-core'
                        scm {
                            url 'https://www.github.com/kryptnostic/fhe-core'
                            connection 'https://www.github.com/kryptnostic/fhe-core'
                            developerConnection 'https://www.github.com/kryptnostic/fhe-core'
                        }
                        licenses {
                            license {
                                name 'Creative Commons Attribution-NonCommercial-ShareAlike 4.0 International Public License'
                                url 'https://creativecommons.org/licenses/by-nc-sa/4.0/legalcode'
                                distribution 'repo'
                            }
                        }
                        developers {
                            developer {
                                id 'geekbeast'
                                name 'Matthew Tamayo-Rios'
                                email 'matthew@kryptnostic.com'
                            }
                            developer {
                                id 'hubsmoke'
                                name 'Sina Iman'
                                email 'sina@kryptnostic.com'
                            }
                            developer {
                                id 'nickdhewitt'
                                name 'Nick Hewitt'
                                email 'nick@kryptnostic.com'
                            }
                        }
                    }
                }
            }
        }
    }
}